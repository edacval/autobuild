_commit="4b3dad8eec3e238b1cadc809acf8e0e26deb9eb3"
pkgname=sickrage
pkgver=20170403
pkgrel=1
pkgdesc='Automatic torrent/nzb video library manager for TV shows'
arch=('any')
url="https://github.com/SickRage/SickRage"
license=('GPL3')
depends=('python2-lxml' 'python2-mako' 'python2-cheetah' 'python2-lockfile' 'python2-notify' 'python2-pyopenssl' 'unrar')
makedepends=('git')
provides=('sickrage')
options=('!strip')
install='sickrage.install'
source=("${pkgname}::git://github.com/SickRage/SickRage.git"
        'sickrage.service' 'sickrage.tmpfile' 'sickrage.install')
sha256sums=('SKIP'
            '59b128f991b71e7c5ce1d10fd7d495e4c2ad10d994d50ea7dbd0bcffeb79a99f'
            'b9c990194d45346217e780b3033ace7c04f17b7895c53b06c2e93d60af7b777a'
            '3327d9e857ba648a4a90a3482e7b08b55c5cb879c0e7441598f80a18e7e2a2cc')

pkgver() {
  cd ${pkgname}
  git log -1 --format="%cd" --date=short | sed 's|-||g'
}

package() {
  install -d -m755 "$pkgdir"/opt/sickrage/{code,data}

  cp -rp sickrage/* "$pkgdir"/opt/sickrage/code

  install -D -m644 sickrage.service "$pkgdir"/usr/lib/systemd/system/sickrage.service
  install -D -m644 sickrage.tmpfile "$pkgdir"/usr/lib/tmpfiles.d/sickrage.conf
}
