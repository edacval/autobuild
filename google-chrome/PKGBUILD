pkgname=google-chrome
pkgver="57.0.2987.133"
pkgrel=1
pkgdesc="An attempt at creating a safer, faster, and more stable browser (Stable Channel)"
arch=('x86_64')
url="https://www.google.com/chrome"
license=('custom:chrome')
depends=('alsa-lib' 'gconf' 'gtk2' 'libxss' 'libxtst' 'nss' 'ttf-liberation')
options=('!emptydirs' '!strip')
install="${pkgname}.install"
source=("google-chrome-stable_${pkgver}_amd64.deb::https://dl.google.com/linux/direct/google-chrome-stable_current_amd64.deb"
        "LICENSE.md"
        'google-chrome-stable.sh')
sha256sums=('4a62a77b3c7960f9313d41a40f645b7178d6a8b91f9f1141d7664026692ef63d'
            '212dfb357426087000e45ff726f651188776758e0d3b7f48341f796f994b1f48'
            '58e65f197d141707546c784c69e3818a1c18212d14148565d71ad9b85525a0d6')

package() {
  bsdtar -xf data.tar.xz -C "$pkgdir/"
  gzip "$pkgdir"/usr/share/man/man1/google-chrome.1

  install -D -m755 google-chrome-stable.sh "$pkgdir"/usr/bin/google-chrome-stable
  install -D -m644 "$pkgdir"/opt/google/chrome/product_logo_256.png "$pkgdir"/usr/share/pixmaps/google-chrome.png
  install -D -m644 LICENSE.md "$pkgdir"/usr/share/licenses/google-chrome/LICENSE

  sed -i "/Exec=/i\StartupWMClass=Google-chrome" "$pkgdir"/usr/share/applications/google-chrome.desktop

  rm -r `find "$pkgdir" -name "cron*" -type d`
  rm `find "$pkgdir" -name "product_logo_*.png" -type f`
}
